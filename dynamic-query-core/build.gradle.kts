plugins {
    antlr
}

dependencies {
    // ANTLR
    val antlrVersion: String by project
    antlr("org.antlr:antlr4:$antlrVersion")

    // JPA
    val jakartaPersistenceApiVersion: String by project
    implementation("jakarta.persistence:jakarta.persistence-api:$jakartaPersistenceApiVersion")
}

tasks {
    compileKotlin {
        dependsOn(generateGrammarSource)
    }

    compileTestKotlin {
        dependsOn(generateTestGrammarSource)
    }

    sourcesJar {
        dependsOn(generateGrammarSource)
    }

    jacocoTestReport {
        // Exclude the Dynamic Query's DSL files from the JaCoCo reports since such source is
        // generated by the ANTLR (without the Generated annotation)
        val filteredFiles = classDirectories.files.map {
            fileTree(it).exclude("br/com/dillmann/dynamicquery/core/grammar/dsl/**")
        }

        classDirectories.setFrom(filteredFiles)
    }
}
